let span = 1h;
let bin_size = 1m;
let WWW = AzureDiagnostics
| where ResourceProvider == "MICROSOFT.CDN" and Category == "FrontDoorAccessLog" and TimeGenerated > now(-span);
let WAF = AzureDiagnostics
| where ResourceProvider == "MICROSOFT.CDN" and Category == "FrontDoorWebApplicationFirewallLog" and TimeGenerated > now(-span);
WWW
| join kind=leftouter WAF on $left.trackingReference_s == $right.trackingReference_s 
| extend 
  logged_b=action_s1 =~ 'Log' or action_s1 =~ 'logandscore',
  blocked_b=action_s1 =~ 'Block'
| project TrackingReference=trackingReference_s, TimeGenerated, RuleSet=ruleName_s1, HttpStatus=httpStatusCode_s, Action=action_s1, PolicyMode=policyMode_s1, Country=clientCountry_s, POP=pop_s, CacheStatus=cacheStatus_s,
  RateLimited=httpStatusCode_s == '429' and blocked_b, 
  Blocked=blocked_b,
  Logged=logged_b,
  Cached=cacheStatus_s in~ ('HIT', 'PARTIAL_HIT', 'REMOTE_HIT'),
  Bot_Blocked=ruleName_s1 startswith "Microsoft_BotManagerRuleSet" and blocked_b,
  Bot_Logged=ruleName_s1 startswith "Microsoft_BotManagerRuleSet" and logged_b,
  DRS_Blocked=ruleName_s1 startswith "Microsoft_DefaultRuleSet" and blocked_b,
  DRS_Logged=ruleName_s1 startswith "Microsoft_DefaultRuleSet" and logged_b,
  Custom_Blocked=ruleName_s1 !startswith "Microsoft_DefaultRuleSet" and ruleName_s1 !startswith "Microsoft_BotManagerRuleSet" and blocked_b
| make-series 
  Requests=dcount(TrackingReference),
  Cached=dcountif(TrackingReference, Cached),
  RateLimited=dcountif(TrackingReference, RateLimited),
  Logged=dcountif(TrackingReference, Logged), 
  Blocked=dcountif(TrackingReference, Blocked), 
  BotBlocked=dcountif(TrackingReference, Bot_Blocked),
  BotLogged=dcountif(TrackingReference, Bot_Logged),
  DRS_Blocked=dcountif(TrackingReference, DRS_Blocked),
  DRS_Logged=dcountif(TrackingReference, DRS_Logged),
  Custom_Blocked=dcountif(TrackingReference, Custom_Blocked)
  on TimeGenerated in range(ago(span), now(), bin_size) 
| mvexpand TimeGenerated, Requests, Cached, RateLimited,  Logged, Blocked, BotBlocked, BotLogged, DRS_Blocked, DRS_Logged, Custom_Blocked
| project todatetime(TimeGenerated), toint(Requests), toint(Cached), toint(RateLimited), toint(Logged), toint(Blocked), toint(BotBlocked), toint(BotLogged), toint(DRS_Blocked), toint(DRS_Logged), toint(Custom_Blocked)
| render timechart